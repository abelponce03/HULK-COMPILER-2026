Program      -> TopLevel Program | ε
TopLevel     -> FunctionDef | TypeDef | TerminatedStmt
TerminatedStmt -> Stmt SEMICOLON
Stmt         -> Expr | WhileStmt | ForStmt | BlockStmt
FunctionDef  -> FUNCTION IDENT LPAREN ArgIdList RPAREN TypeAnnotation FunctionBody
TypeDef      -> TYPE IDENT TypeParams TypeInheritance LBRACE TypeBody RBRACE
TypeParams   -> LPAREN ArgIdList RPAREN | ε
TypeInheritance -> INHERITS IDENT TypeBaseArgs | ε
TypeBaseArgs -> LPAREN ArgList RPAREN | ε
TypeBody     -> TypeMember TypeBody | ε
TypeMember   -> IDENT TypeMemberTail
TypeMemberTail -> TypeAnnotation TypeMemberTail' | LPAREN ArgIdList RPAREN TypeAnnotation MethodBody
TypeMemberTail' -> SEMICOLON | ASSIGN Expr SEMICOLON
AttributeDef -> IDENT ASSIGN Expr SEMICOLON
MethodDef    -> IDENT LPAREN ArgIdList RPAREN FunctionBody
MethodBody   -> ARROW Expr SEMICOLON | BlockStmt
ArgIdList    -> ArgId ArgIdListTail | ε
ArgIdListTail-> COMMA ArgId ArgIdListTail | ε
ArgId        -> IDENT TypeAnnotation
FunctionBody -> ARROW Expr SEMICOLON | BlockStmt
WhileStmt    -> WHILE Expr WhileBody
WhileBody    -> BlockStmt | Expr
ForStmt      -> FOR LPAREN IDENT IN Expr RPAREN ForBody
ForBody      -> BlockStmt | Expr
BlockStmt    -> LBRACE StmtList RBRACE
StmtList     -> TerminatedStmt StmtList | ε
Expr         -> OrExpr | IfExpr | LetExpr
LetExpr      -> LET VarBindingList IN LetBody
LetBody      -> Expr | WhileStmt | ForStmt | BlockStmt
VarBindingList -> VarBinding VarBindingListTail
VarBindingListTail -> COMMA VarBinding VarBindingListTail | ε
VarBinding   -> IDENT TypeAnnotation ASSIGN Expr
TypeAnnotation -> COLON IDENT | ε
IfExpr       -> IF LPAREN Expr RPAREN IfBody ElifList ELSE IfBody
ElifList     -> ElifBranch ElifList | ε
ElifBranch   -> ELIF LPAREN Expr RPAREN IfBody
IfBody       -> BlockStmt | Expr
OrExpr       -> AndExpr OrExpr'
OrExpr'      -> OR AndExpr OrExpr' | ε
AndExpr      -> CmpExpr AndExpr'
AndExpr'     -> AND CmpExpr AndExpr' | ε
CmpExpr      -> ConcatExpr CmpExpr'
CmpExpr'     -> LT ConcatExpr CmpExpr'
              | GT ConcatExpr CmpExpr'
              | LE ConcatExpr CmpExpr'
              | GE ConcatExpr CmpExpr'
              | EQ ConcatExpr CmpExpr'
              | NEQ ConcatExpr CmpExpr'
              | IS IDENT CmpExpr'
              | ε
ConcatExpr   -> AddExpr ConcatExpr'
ConcatExpr'  -> CONCAT AddExpr ConcatExpr'
              | CONCAT_WS AddExpr ConcatExpr'
              | ε
AddExpr      -> Term AddExpr'
AddExpr'     -> PLUS Term AddExpr'
              | MINUS Term AddExpr'
              | ε
Term         -> Factor Term'
Term'        -> MULT Factor Term'
              | DIV Factor Term'
              | MOD Factor Term'
              | ε
Factor       -> Power Factor'
Factor'      -> POW Power Factor'
              | ε
Power        -> Unary
Unary        -> MINUS Unary
              | Primary AsExpr
AsExpr       -> AS IDENT AsExpr | ε
Primary      -> NUMBER
              | STRING
              | TRUE
              | FALSE
              | IDENT PrimaryTail
              | LPAREN Expr RPAREN
              | NEW IDENT LPAREN ArgList RPAREN
              | SELF PrimaryTail
              | BASE LPAREN ArgList RPAREN
PrimaryTail  -> LPAREN ArgList RPAREN PrimaryTail
              | DOT IDENT PrimaryTail
              | ASSIGN_DESTRUCT Expr
              | ASSIGN Expr
              | ε
ArgList      -> Expr ArgListTail
              | ε
ArgListTail  -> COMMA Expr ArgListTail
              | ε

